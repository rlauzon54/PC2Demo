    5 ARUN : LOCK : RANDOM : CLEAR : DIM A$(1)*56: DEGREE 
   10 WAIT 0:K=0: RESTORE 50: FOR II=1 TO 9: READ A$(1): PRINT A$(1)
   15 FOR J=1 TO 60:A$=INKEY$ : IF A$<>"" LET J=60
   20 NEXT J
   22 IF A$="" THEN 35
   25 FOR J=1 TO 9: IF MID$ ("12345ABCD",J,1)=A$ LET K=J:J=9
   30 NEXT J
   31 IF K CLS : ON K GOSUB 100,600,800,300,500,400,700,900,2000: GOTO 10
   35 NEXT II: IF RND (25)>2 THEN 10
   40 K=RND (9): IF K<4 OR K>5 THEN 31
   45 GOTO 10
   50 DATA "1 - LCD display & graphics","2 - Clock","3 - Beeper"
   55 DATA "4 - Phone index","5 - Loan payment","A - Pie Chart"
   58 DATA "B - 3-D Graphics Plotting","C - American Flag","D - Features list"
  100 CLS :A$(1)="08080808496B3E3E1C1C0808"
  101 WAIT 20: FOR I=1 TO 3: CLS : GPRINT A$(1): CLS : GPRINT 0: NEXT I: GPRINT A$(1);
  102 A$(0)="Full Screen Control"
  103 CURSOR 3: WAIT 12: FOR I=1 TO LEN A$(0): PRINT MID$ (A$(0),I,1);: NEXT I: WAIT 40: PRINT " ";
  104 WAIT 20: FOR I=1 TO 4: CURSOR 8: PRINT "      ";: CURSOR 8: PRINT "Screen";: NEXT I: WAIT 90: GPRINT 0;: CURSOR 8
  105 WAIT 20: PRINT "      ";
  106 A$(0)="Cursor": FOR I=LEN A$(0) TO 1 STEP -1: CURSOR 7+I: PRINT MID$ (A$(0),I,1);: NEXT I: GOSUB 9000
  107 WAIT 20: FOR I=1 TO 4: CURSOR 8: PRINT "      ";: CURSOR 8: PRINT A$(0);: NEXT I: WAIT 60: GPRINT 0;
  108 CURSOR 8: PRINT "Screen Graphics ";
  109 WAIT 0: GCURSOR 0: FOR I=0 TO 144: GPRINT (127-POINT I);: NEXT I
  110 FOR J=1 TO 2: FOR I=144 TO 0 STEP -1: GCURSOR I: GPRINT (127-POINT I);: NEXT I: NEXT J: WAIT 180: GCURSOR 0: GPRINT 0;
  111 WAIT 0: GCURSOR 0: FOR I=155 TO 81 STEP -1: GPRINT (127-POINT I);: NEXT I: GCURSOR 71: PRINT "  ";: WAIT 180: GCURSOR 0: GPRINT 0;
  112 WAIT 0: GCURSOR 0: FOR I=0 TO 71: GPRINT POINT (I+76);: NEXT I: WAIT 180: GPRINT 0;
  200 WAIT 0
  201 A$(1)="0000105D761C765D100000":A$(0)="00001030382878687C6E7E6E7C687828383010
  202 CLS : FOR I=155 TO 101 STEP -1: GOSUB 210: NEXT I
  203 FOR I=0 TO 20: GCURSOR I: GPRINT A$(0);: NEXT I
  204 FOR I=101 TO 147 STEP 2: GOSUB 210: NEXT I: FOR J=1 TO 2: FOR I=147 TO 131 STEP -2: GOSUB 210: NEXT I
  205 FOR I=131 TO 147 STEP 2: GOSUB 210: NEXT I: NEXT J: FOR I=147 TO 135 STEP -2: GOSUB 210: NEXT I: GCURSOR 138: GPRINT "5E7E1C7E5E
  206 FOR I=39 TO 138 STEP 3: GCURSOR I: GPRINT "0000002838101010101010";: NEXT I
  207 GCURSOR 141: PRINT "  ";: FOR I=10 TO 5 STEP -1: BEEP 1,I,2*I: NEXT I
  208 RESTORE 220: FOR I=1 TO 7: READ A,B$: GCURSOR A: GPRINT B$;: NEXT I: WAIT 90: GPRINT 0;
  209 WAIT 0: FOR I=21 TO 155 STEP 2: GCURSOR I: GPRINT A$(0);: NEXT I: RETURN 
  210 GCURSOR I: GPRINT A$(1);: RETURN 
  220 DATA 140,"1C1C1C"
  221 DATA 139,"3E3E3E3E3E"
  222 DATA 139,"3E3E363E3E"
  223 DATA 138,"3E7F7F777F7F3E"
  224 DATA 138,"3E7F6363637F3E"
  225 DATA 138,"3E41414141413E"
  226 DATA 138,"00000000000000"
  300 X$="&": INPUT "Enter name or number: ";X$
  301 WAIT : RESTORE 390:Y=1:Z=2: IF VAL X$>0 LET Y=2:Z=1
  302 READ A$,B$: IF LEFT$ (@$(Y),LEN X$)=X$ PRINT @$(Y);": ";@$(Z)
  303 IF @$(Y)="*" PRINT "*** LIST END ***": RETURN 
  304 GOTO 302
  390 DATA "JOHN SMITH","1234","BOB JONES","1111","JOHN YORK","2222","BOB SAND","4321
  395 DATA "BILL DOUGLAS","3333","PAUL MERNAN","9876","DAVE POTTS","6789"
  396 DATA "*","*"
  400 GRAPH : GLCURSOR (110,-150): SORGN 
  401 COLOR 0:A=-20:B=90: GOSUB 410: COLOR 2:A=B:B=130: GOSUB 410: COLOR 3:A=B:B=170: GOSUB 410
  402 COLOR 1:A=B:B=280: GOSUB 410: COLOR 2:A=B:B=340: GOSUB 410: COLOR 0
  403 CSIZE 1: GLCURSOR (-65,40): LPRINT "16%": GLCURSOR (33,47): LPRINT "33%": GLCURSOR (57,-22): LPRINT "11%": GLCURSOR (25,-65)
  404 LPRINT "12%": GLCURSOR (-62,-47): LPRINT "28%": GLCURSOR (0,-200): RETURN 
  410 D=.5*(A+B):I=4*SIN D:J=4*COS D:P=I+100*SIN A:Q=J+100*COS A:R=I+100*SIN B:S=J+100*COS B
  411 LINE (R,S)-(I,J)-(P,Q)
  412 FOR C=A+10 TO B STEP 10:R=I+100*SIN C:S=J+100*COS C: LINE (P,Q)-(R,S):P=R:Q=S: NEXT C: RETURN 
  500 WAIT 180: INPUT "Principal amount? $";A
  510 INPUT "Annual interest rate? ";I
  511 INPUT "Interest periods/year? ";M
  512 R=I/(M*100)
  520 INPUT "Number of payments? ";N
  530 P=A*R/(1-(1+R)^-N):P=.01*INT (100*P+.5)
  540 PRINT "Payment is $";STR$ P
  550 S=P*N: PRINT "Sum of payments $";STR$ S
  560 T=S-A: PRINT "Sum of interest $";STR$ T
  570 RETURN 
  600 WAIT 0: PRINT " The time is";
  601 FOR I=1 TO 40: CURSOR 12:T$=STR$ (TIME ):H$=MID$ (T$,4,2):M$=MID$ (T$,7,2):S$=MID$ (T$,9,2):H=VAL H$
  602 IF LEN S$<2 LET S$=S$+"0": GOTO 602
  603 A$="A.M.": IF H>11 LET A$="P.M."
  604 IF H>12 LET H=H-12
  605 PRINT H;":";M$;":";S$;" ";A$:B$=INKEY$ : IF B$<>"" LET I=100
  606 NEXT I: RETURN 
  700 COLOR 1: GRAPH : GLCURSOR (0,-150): SORGN : ON ERROR GOTO 790: LINE (0,145)-(216,-100),,,B
  701 CSIZE 1: GLCURSOR (2,135): LPRINT " Potential Energy near a Black Hole": LINE (0,130)-(216,130)
  702 FOR X=-10 TO 10: FOR Y=-10 TO 10: GOSUB 750: IF Y<>-10 LINE (A,B)-(C,D)
  704 A=C:B=D: NEXT Y: NEXT X
  711 FOR Y=-10 TO 10: FOR X=-10 TO 10: GOSUB 750: IF X<>-10 LINE (A,B)-(C,D)
  713 A=C:B=D: NEXT X: NEXT Y: GLCURSOR (0,-200): ON ERROR GOTO 5: RETURN 
  750 Z=-2/âˆš(X*X+Y*Y)
  751 C=8+5*(X+10)+5*(Y+10):D=50-2.5*(X+10)+2.5*(Y+10)+50*Z: RETURN 
  790 Z=-2.5: GOTO 751
  800 RESTORE 804
  801 READ A,B: IF A=0 RETURN 
  802 IF A=255 FOR I=0 TO B: NEXT I: GOTO 801
  803 BEEP 1,A,B*7000/A: GOTO 801
  804 DATA 42,.7,42,.7,255,30,42,.7,42,.7,255,30,42,6,42,.5,42,.5,255,40
  805 DATA 26,2,26,2,30,1,32,1,32,2,35,1,32,1,32,6,255,16
  806 DATA 35,1,32,1,32,2,35,1,32,1,26,2,32,1,26,1,30,6,37,4,255,8
  807 DATA 37,1,37,2,40,1,37,1,37,2,40,1,37,1,30,6,255,16
  808 DATA 32,1,36,1,32,1,26,2,26,1,22,2,22,2,36,6,255,24
  809 DATA 26,2,26,2,30,1,32,1,32,2,35,1,32,1,32,6,255,16
  810 DATA 35,1,32,1,32,2,35,1,32,1,30,1,32,1,37,1,42,1,37,6,42,4,255,8
  811 DATA 42,1,42,2,45,1,42,1,35,2,37,1,42,1,26,6,255,16
  812 DATA 42,1,37,1,32,1,26,1,42,1,37,1,32,1,26,1,42,1,37,1,32,1,37,6,42,2,255,5
  813 DATA 42,1,0,0
  900 GRAPH : GLCURSOR (7,-104): SORGN : LINE (0,0)-(180,104),,3,B
  901 LINE (0,48)-(80,104),,1,B
  902 COLOR 3:L=0: FOR I=0 TO 32 STEP 16: GOSUB 910: NEXT I
  903 L=80: FOR I=48 TO 88 STEP 16: GOSUB 910: NEXT I
  904 COLOR 1: FOR I=48 TO 100 STEP 4: LINE (0,I)-(80,I),1: LINE (80,I+2)-(0,I+2),1: NEXT I
  905 LINE (-7,-100)-(-2,110),0,0,B: GLCURSOR (0,-200): GOTO 800
  910 FOR J=I TO I+8 STEP 2: LINE (L,J)-(180,J): NEXT J: RETURN 
 2000 RESTORE 2499: TEXT 
 2002 READ A,B,A$(1): IF A=99 RETURN 
 2003 IF A=0 LF B: GOTO 2002
 2005 CSIZE A: LPRINT TAB B;A$(1): GOTO 2002
 2499 DATA 3,1,"RADIO SHACK",0,1,""
 2500 DATA 5,2,"PC-2",3,3,"Features",0,1,""
 2501 DATA 2,0,"ARUN: Automatic    program start     on power-up",0,1,""
 2502 DATA 2,0,"LOCK: Prevents     user from",2,1,"changing mode",0,1,""
 2504 DATA 2,0,"GPRINT: Graphics   on LCD display",0,1,""
 2505 DATA 2,0,"Accurate internal  clock accessible  from program",0,1,""
 2506 DATA 2,0,"Fully programable  beeper",0,1,""
 2507 DATA 2,0,"Expandable memory, plug-in program   modules, and FAST"
 2508 DATA 2,1,"high-powered",2,1,"problem-solving",0,2,""
 2595 DATA 2,0,"The PC-2 Printer-  Plotter",0,1,""
 2596 DATA 2,0,"PRINTS full",2,1,"character set in  four colors and",2,1,"nine sizes
 2597 DATA 0,1,"",2,0,"PLOTS with .2 mm   resolution",0,2,""
 2598 DATA 3,5,"the",3,1,"Radio Shack",0,1,"",5,2,"PC-2",2,1,"the future today!"
 2599 DATA 0,3,"",99,0,""
 9000 FOR Z=1 TO 90: NEXT Z: RETURN 
 9999 END
